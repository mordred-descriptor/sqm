#!/bin/csh -f

# check if slko files for DFTB are installed
../../check_slko_files.x DFTB3
if( $status > 0) then
    # no slko files - do not run DFTB tests
    exit(0)
endif

set SQM = $AMBERHOME/bin/sqm
set SPDIFF = ../../dacdif
set GODIFF = "../../dacdif -a 0.02"
set SPTESTS = (h2o.sp cysdip.sp lignin.sp)
set GOTESTS = (h2o.go cysdip.go)

# single point tests
foreach i ($SPTESTS)
    set input = $i.in
    set output = $i.out
    $SQM -O -i $input -o $output || goto error
    $SPDIFF $output.save $output
end

# geometry optimization tests
# do not check the electronic energy and
# core repulsion energy, they are too sensitive!
# Also, there may be a different number of optimization steps
# on different machines, hence remove xmin output
# Also, do not check the final geometry since small numerical
# differences on different platforms can result in different
# Cartesian coordinates although the internal coordinates are OK
foreach i ($GOTESTS)
    set input = $i.in
    set output = $i.out
    $SQM -O -i $input -o $output || goto error
    grep -v 'Electronic energy' $output > tmp
    grep -v 'Core-core repulsion' tmp > tmp2
    grep -v 'DIPOLE' tmp2 > tmp
    grep -v 'xmin' tmp > tmp2
    sed -e "/Final Structure/,/Calculation Completed/d" tmp2 > $output
    rm tmp tmp2
    $GODIFF $output.save $output
end

exit(0)

error:
echo "  ${0}:  Program error"
exit(1)
